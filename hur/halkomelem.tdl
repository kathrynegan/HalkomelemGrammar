;;; -*- Mode: TDL; Coding: utf-8 -*-
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Grammar of Halkomelem
;;; created at:
;;;	Fri May 17 21:06:04 UTC 2013
;;; based on Matrix customization system version of:
;;;	Fri Apr 26 22:56:20 UTC 2013
;;;
;;; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Type assigning empty mod list. Added to basic types for nouns, verbs and determiners.

non-mod-lex-item := lex-item &
  [ SYNSEM.LOCAL.CAT.HEAD.MOD < > ].

;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Matrix Type Addenda
;;;;;;;;;;;;;;;;;;;;;;;;;

+npc :+ [ CASE case,
    CASE-MARKED bool ].

head :+ [ FORM form,
    AUX bool ].

inflected :+ [ OBJ-SUFFIX-FLAG luk,
    MC-SUBJ-SUFFIX-FLAG luk,
    SUBJ-SUFFIX-FLAG luk,
    TRANS-SUFFIX-FLAG luk,
    ADVERB-FLAG luk ].

infl-satisfied :+ [ OBJ-SUFFIX-FLAG na-or-+,
    MC-SUBJ-SUFFIX-FLAG na-or-+,
    SUBJ-SUFFIX-FLAG na-or-+,
    TRANS-SUFFIX-FLAG na-or-+,
    ADVERB-FLAG na-or-+ ].

png :+ [ PER person,
    NUM number,
    GEND gender ].

basic-head-comp-phrase :+ [ HEAD-DTR.SYNSEM.LOCAL.CAT.MC #mc,
    SYNSEM.LOCAL.CAT [ MC #mc,
		       VC #vc ],
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VC #vc ].

basic-head-mod-phrase-simple :+ [ SYNSEM.LOCAL.CAT.MC #mc,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.MC #mc ].

basic-head-opt-subj-phrase :+ [ HEAD-DTR.SYNSEM.LOCAL.CAT.VC +,
				SYNSEM.LOCAL.CAT.INIT na ].

adj-head-int-phrase :+ [ SYNSEM.LOCAL.CAT.VC #vc,
    HEAD-DTR.SYNSEM.LOCAL.CAT.VC #vc ].

head-adj-int-phrase :+ [ SYNSEM.LOCAL.CAT.VC #vc,
    HEAD-DTR.SYNSEM.LOCAL.CAT.VC #vc ].

; Introducing VC keeps track whether main-verb is present in cluster

cat :+ [ VC luk,
	 INIT luk ].

event :+ [ EV evid ].

lex-rule :+ [ SYNSEM [ LOCAL.CAT.VC #vc,
		       SCOPE-ARG #scope ],
    DTR.SYNSEM [ LOCAL.CAT.VC #vc,
		 SCOPE-ARG #scope ] ].
headed-phrase :+ 
  [ SYNSEM.SCOPE-ARG #scope, 
    HEAD-DTR.SYNSEM.SCOPE-ARG #scope ]. 
 
n-coord-phrase :+ [ SYNSEM.SCOPE-ARG - ]. 
np-coord-phrase :+ [ SYNSEM.SCOPE-ARG - ]. 
vp-coord-phrase :+ [ SYNSEM.SCOPE-ARG + ]. 
s-coord-phrase :+ [ SYNSEM.SCOPE-ARG + ].



;;;;;;;;;;;;;;
;;; Features
;;;;;;;;;;;;;;

;;; Case

case := *top*.
cor := case.  ; core
obl := case.  ; obl

;;; Person

person := *top*.
3rd := person.
non-3rd := person.
2nd := non-3rd.
1st := non-3rd.

;;; Number

number := *top*.
sg := number.
pl := number.

;;; Gender

gender := *top*.
fem := gender.
masc := gender.

;;; Form

form := *top*.
nform := form.
nonfinite := form.
nom+subord := nonfinite.
nom := nom+subord.
subord := nom+subord & subord+finite.
non3 := nonfinite & non3+finite.
finite := non3+finite & subord+finite.
non-nom := form.
non3+finite := non-nom.
subord+finite := non-nom.



;;; Tense

past := tense.
present := tense.
future := tense.

;;; Aspect

dur := aspect.
dur-only := dur.
prog := aspect.
prog-only := prog.
still := aspect.
still-only := still.
emph := aspect.
emph-only := emph.
no-aspect := aspect.
still-dur := dur & still.
still-dur-only := still-dur.
emph-dur := dur & emph.
emph-dur-only := emph-dur.
prog-dur := dur & prog.
prog-dur-only := prog-dur.
still-prog := prog & still.
still-prog-only := still-prog.
emph-prog := prog & emph.
emph-prog-only := emph-prog.
emph-still := still & emph.
emph-still-only := emph-still.
still-prog-dur := prog-dur & still.
still-prog-dur-only := still-prog-dur.
emph-prog-dur := prog-dur & emph.
emph-prog-dur-only := emph-prog-dur.
emph-still-dur := still-dur & emph.
emph-still-dur-only := emph-still-dur.
emph-still-prog := still-prog & emph.
emph-still-prog-only : = emph-still-prog.
emph-still-prog-dur := still-prog-dur & emph.

;;; Evid

evid := *top*.
indirect := evid.
assumed : = indirect.

;;; Demonstrative Predsort

demonstrative_a_rel := predsort.
proximal+dem_a_rel := demonstrative_a_rel.
distal+dem_a_rel := demonstrative_a_rel.
remote+dem_a_rel := distal+dem_a_rel.

;;;;;;;;;;;;;;;;;;;
;;; Lexical Types
;;;;;;;;;;;;;;;;;;;

;;; Nouns

noun-lex := basic-noun-lex & basic-one-arg & no-hcons-lex-item & non-mod-lex-item &
  [ ARG-ST < #spr >,
    SYNSEM [ SCOPE-ARG -,
	     LOCAL.CAT [ HEAD.FORM nform,
		       VAL [ COMPS < >,
			   SUBJ < >,
			   SPEC < >,
			   SPR < #spr &
				 [ LOCAL.CAT.HEAD det,
				   OPT - ] > ] ] ] ].

masc-noun-lex := noun-lex &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.PNG.GEND masc ].

fem-noun-lex := noun-lex &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.PNG.GEND fem ].

basic-noun-noun-lex := noun-lex &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.PNG.PER 3rd ].

fem-basic-noun-lex := fem-noun-lex & basic-noun-noun-lex.

masc-basic-noun-lex := masc-noun-lex & basic-noun-noun-lex.

;;; Verbs

verb-lex := non-mod-lex-item &
  [ SYNSEM [ SCOPE-ARG +, 
	     LOCAL.CAT [ HEAD verb,
		       HC-LIGHT - ] ] ].

main-verb-lex := verb-lex & basic-verb-lex &
  [ SYNSEM.LOCAL [ CAT [ HEAD.AUX -,
			 VC +,
			 INIT +,
			 VAL [ SPR < >,
			       SPEC < >,
			       SUBJ < #subj > ] ],
		   CONT.HOOK.XARG #xarg ],
    ARG-ST.FIRST #subj &
		 [ LOCAL [ CAT.VAL [ SPR < >,
				     COMPS < > ],
			   CONT.HOOK.INDEX #xarg ] ] ].

aux-lex := verb-lex &
  [ SYNSEM.LOCAL.CAT [ HEAD.AUX +,
		       VC - ] ].

intransitive-verb-lex := main-verb-lex & intransitive-lex-item &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < >,
    ARG-ST.FIRST.LOCAL.CAT.HEAD +np ].

transitive-verb-lex := main-verb-lex & transitive-lex-item &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < #comps >,
    ARG-ST < [ LOCAL.CAT.HEAD +np ],
	     #comps &
	     [ LOCAL.CAT [ VAL [ SPR < >,
				 COMPS < > ],
			   HEAD +np ] ] > ].

cor-intransitive-verb-lex := intransitive-verb-lex &
  [ ARG-ST.FIRST.LOCAL.CAT.HEAD noun &
				[ CASE cor ],
    SYNSEM.LOCAL.CAT.VAL.SUBJ < [ LOCAL.CAT.HEAD.CASE-MARKED + ] > ].

cor-obl-transitive-verb-lex := transitive-verb-lex &
  [ ARG-ST < [ LOCAL.CAT.HEAD noun &
			      [ CASE cor ] ],
	     [ LOCAL.CAT.HEAD adp &
			      [ CASE obl ] ] >,
    SYNSEM.LOCAL.CAT.VAL [ SUBJ < [ LOCAL.CAT.HEAD.CASE-MARKED + ] >,
			   COMPS < [ LOCAL.CAT.HEAD.CASE-MARKED + ] > ] ].

trans-obl-verb-lex := cor-obl-transitive-verb-lex & reg-subj-suffix-rule-dtr & intrans-suffix-rule-dtr & nom-prefix-rule-dtr &
  [ INFLECTED.SUBJ-SUFFIX-FLAG - ].

trans-verb-lex := transitive-verb-lex & trans-suffix-rule-dtr &
  [ INFLECTED [ TRANS-SUFFIX-FLAG -,
		SUBJ-SUFFIX-FLAG - ] ].

intrans-verb-lex := cor-intransitive-verb-lex & reg-subj-suffix-rule-dtr & intrans-suffix-rule-dtr & nom-prefix-rule-dtr &
  [ INFLECTED.SUBJ-SUFFIX-FLAG - ].

clause-embed-lex := main-verb-lex & clausal-second-arg-trans-lex-item & trans-suffix-rule-dtr &
 [ SYNSEM.LOCAL.CAT.VAL.COMPS < #comps >,
   ARG-ST < [ LOCAL.CAT.HEAD +np ],
	     #comps &
	    [ LOCAL [ CAT [ HEAD comp & 
			       [ FORM subord ],
			    VAL [ SUBJ < >,
				  COMPS < > ] ],
		      CONT.HOOK.INDEX.SF prop-or-ques ] ] >,
   INFLECTED [ TRANS-SUFFIX-FLAG - ,
	       SUBJ-SUFFIX-FLAG - ] ].
   
prop-embed-lex := clause-embed-lex &
 [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CONT.HOOK.INDEX.SF prop ].
 
ques-embed-lex := clause-embed-lex &
 [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CONT.HOOK.INDEX.SF ques ].

;;; Auxiliaries

arg-comp-aux := aux-lex & basic-two-arg &
  [ SYNSEM.LOCAL [ CAT.VAL [ SPR < >,
			     SPEC < >,
			     COMPS < #comps . #vcomps >,
			     SUBJ < #subj > ],
		   CONT.HOOK.XARG #xarg ],
    ARG-ST < #subj &
	     [ LOCAL [ CAT [ HEAD noun &
				  [ CASE #case ],
			     VAL [ SUBJ < >,
				   SPR < >,
				   SPEC < >,
				   COMPS < > ] ],
		       CONT.HOOK.INDEX #xarg ] ],
	     #comps &
	     [ LOCAL [ CONT.HOOK.XARG #xarg,
		       CAT [ HEAD verb,
			     VAL [ COMPS #vcomps,
				   SUBJ < [ LOCAL.CAT.HEAD.CASE #case ] > ] ] ] ] > ].

init-aux := arg-comp-aux &
  [ SYNSEM.LOCAL.CAT.INIT +,
    ARG-ST < [ ],
	     [ LIGHT +] > ].

sec-aux := arg-comp-aux &
  [ SYNSEM.LOCAL.CAT.INIT - ].

raise-form-aux := arg-comp-aux &
[ SYNSEM.LOCAL.CAT [ HEAD.FORM #form,
		     VAL.COMPS.FIRST.LOCAL.CAT.HEAD.FORM #form ] ].
				   
aux-no-pred := arg-comp-aux & raise-sem-lex-item &
    [ ARG-ST < [ ],
	       [ ] > ].

aux-with-pred := arg-comp-aux & hcons-lex-item &
  [ SYNSEM [ LOCAL.CONT.HCONS <! qeq &
				 [ HARG #harg,
				   LARG #larg ] !>,
	     LKEYS.KEYREL event-relation &
			  [ ARG1 #harg ] ],
    ARG-ST < [ ],
	     [ LOCAL.CONT.HOOK.LTOP #larg ] > ].

full-aux-lex := init-aux & aux-with-pred & nom-prefix-rule-dtr & reg-subj-suffix-rule-dtr &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD.FORM non3+finite,
    INFLECTED.SUBJ-SUFFIX-FLAG - ].

adv-aux-lex := init-aux & aux-with-pred & raise-form-aux & durative-prefix-rule-dtr & emph-prefix-rule-dtr & past-suffix-rule-dtr & progressive-prefix-rule-dtr & still-prefix-rule-dtr &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD.FORM non3+finite ].

nom-comp-aux-lex := init-aux & aux-with-pred & reg-subj-suffix-rule-dtr &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD.FORM nom,
    INFLECTED.SUBJ-SUFFIX-FLAG - ].

neg-aux-lex := init-aux & aux-with-pred & reg-subj-suffix-rule-dtr &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD.FORM nom+subord,
    INFLECTED.SUBJ-SUFFIX-FLAG - ].

rog-aux-lex := sec-aux & aux-no-pred & raise-form-aux &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD.FORM non-nom,
		   CONT.HOOK.INDEX.SF ques  ] ].

mc-aux-lex := sec-aux & aux-no-pred &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD.FORM non3,
    INFLECTED.MC-SUBJ-SUFFIX-FLAG - ].

quot-aux-lex := sec-aux & aux-no-pred & raise-form-aux &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD.FORM finite,
		   CONT.HOOK.INDEX.EV assumed ] ].

fut-aux-lex := sec-aux & aux-no-pred & raise-form-aux &
  [ SYNSEM.LOCAL [ CONT.HOOK.INDEX.E.TENSE future,
		   CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD.FORM subord+finite ] ].

;;; Others

;; Complementizer 

complementizer-lex-item := raise-sem-lex-item & basic-one-arg &
 [ SYNSEM [ SCOPE-ARG -,
	    LOCAL [ CAT [ HEAD comp,
			VAL [ COMPS < #comps >,
			      SUBJ < > ] ],
		  CONT.HOOK.INDEX.SF prop-or-ques ] ],
   ARG-ST < #comps &
	    [ LOCAL [ CAT [ HEAD verb & 
			       [ FORM subord ],
			    VAL [ SUBJ < >,
				  COMPS < > ] ],
		      CONT.HOOK.INDEX.SF prop-or-ques ] ] > ].



;;; Modifiers

adjective-lex := basic-adjective-lex & intersective-mod-lex & norm-ltop-lex-item & emph-prefix-rule-dtr & reg-subj-suffix-rule-dtr &
  [ SYNSEM [ SCOPE-ARG -,
	     LOCAL [ CAT [ HEAD [ MOD < [ LOCAL.CAT [ HEAD noun,
						      VAL.SPR cons ] ] > ],
			   VAL [ SUBJ < >,
				 COMPS < >,
				 SPR < >,
				 SPEC < > ],
			   POSTHEAD - ],
		     CONT.HOOK [ XARG #xarg,
				 INDEX #arg0 ] ],
	     LKEYS.KEYREL [ ARG0 #arg0,
			    ARG1 #xarg ] ] ].

adverb-lex := basic-adverb-lex & intersective-mod-lex
  [ SYNSEM [ LOCAL.CAT [ VAL [ SPR < >,
			       SUBJ < >,
			       COMPS < >,
			       SPEC < > ] ] ],
    INFLECTED [ ADVERB-FLAG - ] ].

;;; Case-marking adpositions
;;; Case marking adpositions are constrained not to
;;; be modifiers.

case-marking-adp-lex := basic-one-arg & raise-sem-lex-item &
  [ SYNSEM.LOCAL.CAT [ HEAD adp &
			    [ CASE #case,
			      MOD < > ],
		       VAL [ SPR < >,
			     SUBJ < >,
			     COMPS < #comps >,
			     SPEC < > ] ],
    ARG-ST < #comps &
	     [ LOCAL.CAT [ VAL.SPR < >,
			   HEAD noun &
				[ CASE #case,
				  CASE-MARKED - ] ] ] > ].

;;; Determiners
;;; SPEC is non-empty, and already specified by basic-determiner-lex.

determiner-lex-supertype := norm-hook-lex-item & basic-zero-arg & non-mod-lex-item &
  [ SYNSEM [ LOCAL [ CAT [ HEAD det &
				[ MOD < > ],
			   VAL[ SPEC.FIRST.LOCAL.CONT.HOOK [ INDEX #ind,
							     LTOP #larg ],
				SPR < >,
				SUBJ < >,
				COMPS < > ] ],
		     CONT [ HCONS < ! qeq &
				    [ HARG #harg,
				      LARG #larg ] ! >,
			    RELS < ! [ PRED "exist_q_rel" ],
				       #altkey ! > ] ],
	    LKEYS.ALTKEYREL #altkey & arg1-ev-relation &
			   [ LBL #larg,
			     ARG1 #ind ],
	    LKEYS.KEYREL quant-relation &
		       [ ARG0 #ind,
			 RSTR #harg ] ] ].
		     
demonstrative-det-lex:= determiner-lex-supertype &
	[ SYNSEM.LOCAL.CAT.VAL.SPEC.FIRST.LOCAL.CONT.HOOK.INDEX.COG-ST activ+fam ].
								
article-det-lex := determiner-lex-supertype.

p-determiner-lex := determiner-lex-supertype &
  [ SYNSEM.LKEYS.ALTKEYREL.PRED proximal+dem_a_rel ].

n-determiner-lex := determiner-lex-supertype &
  [ SYNSEM.LKEYS.ALTKEYREL.PRED distal+dem_a_rel ].

r-determiner-lex := determiner-lex-supertype &
  [ SYNSEM.LKEYS.ALTKEYREL.PRED remote+dem_a_rel ].

f-determiner-lex := determiner-lex-supertype &
  [ SYNSEM.LOCAL.CAT.VAL.SPEC.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.GEND fem ].

m-determiner-lex := determiner-lex-supertype &
  [ SYNSEM.LOCAL.CAT.VAL.SPEC.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.GEND masc ].

pl-determiner-lex := determiner-lex-supertype &
  [ SYNSEM.LOCAL.CAT.VAL.SPEC.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.NUM pl ].
  
m_p-dem-lex := m-determiner-lex & p-determiner-lex & demonstrative-det-lex.

m_p-art-lex := m-determiner-lex & p-determiner-lex & article-det-lex.
								
f_p-dem-lex := f-determiner-lex & p-determiner-lex & demonstrative-det-lex.

f_p-art-lex := f-determiner-lex & p-determiner-lex & article-det-lex.
  
m_n-dem-lex := m-determiner-lex & n-determiner-lex & demonstrative-det-lex.

m_n-art-lex := m-determiner-lex & n-determiner-lex & article-det-lex.
								
f_n-dem-lex := f-determiner-lex & n-determiner-lex & demonstrative-det-lex.

f_n-art-lex := f-determiner-lex & n-determiner-lex & article-det-lex.
  
m_r-dem-lex := m-determiner-lex r-determiner-lex & demonstrative-det-lex.

m_r-art-lex := m-determiner-lex & r-determiner-lex & article-det-lex.
								
f_r-dem-lex := f-determiner-lex & r-determiner-lex & demonstrative-det-lex.

f_r-art-lex := f-determiner-lex & r-determiner-lex & article-det-lex.
								
pl_p-dem-lex := pl-determiner-lex & p-determiner-lex & demonstrative-det-lex.

pl_p-art-lex := pl-determiner-lex & p-determiner-lex & article-det-lex.
								
pl_n-dem-lex := pl-determiner-lex & n-determiner-lex & demonstrative-det-lex.

pl_n-art-lex := pl-determiner-lex & n-determiner-lex & article-det-lex.

;;;;;;;;;;;;;;;;;;;
;;; Lexical Rules
;;;;;;;;;;;;;;;;;;;

;;; Intermediate rule types

nom-prefix-rule-dtr := word-or-lexrule.
past-suffix-rule-dtr := word-or-lexrule.
durative-prefix-rule-dtr := word-or-lexrule.
emph-prefix-rule-dtr := word-or-lexrule.
intrans-suffix-rule-dtr := word-or-lexrule.
still-prefix-rule-dtr := word-or-lexrule.
progressive-prefix-rule-dtr := word-or-lexrule.
subj-suffix-rule-dtr := word-or-lexrule.
trans-suffix-rule-dtr := word-or-lexrule.
trans-subj-suffix-rule-dtr := subj-suffix-rule-dtr.
reg-subj-suffix-rule-dtr := subj-suffix-rule-dtr.

;;; Lexical rule types

plural-prefix-lex-rule-super := add-only-no-ccont-rule & infl-lex-rule &
  [ DTR basic-noun-noun-lex ].

plural-lex-rule := plural-prefix-lex-rule-super &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.PNG.NUM pl ].

adj-pred-lex-rule-super := const-lex-rule & reg-subj-suffix-rule-dtr &
  [ INFLECTED.SUBJ-SUFFIX-FLAG -,
    DTR adjective-lex ].

adj-pred-lex-rule := adj-pred-lex-rule-super &
  [ SYNSEM.LOCAL [ CAT [ HEAD verb &
			     [ MOD < > ],
		       VAL [ COMPS < >,
			     SPEC < >,
			     SPR < >,
			     SUBJ < [ LOCAL [ CONT.HOOK.INDEX #index,
					      CAT [ HEAD noun,
						    VAL.SPR < > ] ] ] > ] ],
		     CONT [ HOOK.INDEX #arg0 ] ],
    C-CONT [ HOOK [ XARG #xarg,
		    LTOP #ltop ],
	    RELS <! !>, 
	    HCONS <! !> ],
    DTR.SYNSEM [ LOCAL [ CAT [ HEAD adj &
				       [ MOD < [ LOCAL.CONT.HOOK.INDEX #index ] > ] ],
			      CONT.HOOK [ XARG #xarg,
					  LTOP #ltop ] ],
		      LKEYS.KEYREL.ARG0 #arg0 ] ].

trans-suffix-lex-rule-super := add-only-no-ccont-rule &
  [ INFLECTED [ TRANS-SUFFIX-FLAG +,
		SUBJ-SUFFIX-FLAG #subj-suffix ],
    DTR trans-suffix-rule-dtr &
	[ INFLECTED.SUBJ-SUFFIX-FLAG #subj-suffix ] ].

core-trans-lex-rule := infl-lex-rule & trans-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ < [ LOCAL.CAT.HEAD.CASE-MARKED + ] >,
			   COMPS < [ LOCAL.CAT.HEAD.CASE-MARKED + ] > ],
    INFLECTED.OBJ-SUFFIX-FLAG -,
    DTR.ARG-ST #arg-st,
    ARG-ST #arg-st & 
	   < [ LOCAL.CAT.HEAD noun &
			      [ CASE cor ] ],
	     [ LOCAL.CAT.HEAD +npc &
			      [ CASE cor ] ] > ].

obl-trans-lex-rule := const-lex-rule & intrans-suffix-rule-dtr & nom-prefix-rule-dtr & reg-subj-suffix-rule-dtr & trans-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ < [ LOCAL.CAT.HEAD.CASE-MARKED + ] >,
			   COMPS < [ LOCAL.CAT.HEAD.CASE-MARKED + ] > ],
    INFLECTED.OBJ-SUFFIX-FLAG #obj-suffix,
    DTR [ INFLECTED.OBJ-SUFFIX-FLAG #obj-suffix,
	  ARG-ST #arg-st ],
    ARG-ST #arg-st &
	   < [ LOCAL.CAT.HEAD noun &
			     [ CASE cor ] ],
	     [ LOCAL.CAT.HEAD adp &
			     [ CASE obl ] ] > ].

limit-core-trans-lex-rule := core-trans-lex-rule.

basic-core-trans-lex-rule := core-trans-lex-rule.

obj-suffix-lex-rule-super := add-only-no-ccont-rule & nom-prefix-rule-dtr & trans-subj-suffix-rule-dtr &
  [ INFLECTED [ OBJ-SUFFIX-FLAG +,
		SUBJ-SUFFIX-FLAG #subj-suffix,
		TRANS-SUFFIX-FLAG #trans-suffix ],
    DTR core-trans-lex-rule &
	[ INFLECTED [ SUBJ-SUFFIX-FLAG #subj-suffix,
		      TRANS-SUFFIX-FLAG #trans-suffix ] ] ].

1sg-obj-lex-rule := infl-lex-rule & obj-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM sg,
								 PER 1st ] ].

2sg-obj-lex-rule := infl-lex-rule & obj-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM sg,
								 PER 2nd ] ].

1pl-obj-lex-rule := infl-lex-rule & obj-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM pl,
								 PER 1st ] ].

2pl-obj-lex-rule := infl-lex-rule & obj-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM pl,
								 PER 2nd ] ].

3-obj-lex-rule := const-lex-rule & obj-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.PER 3rd ].

comp-obj-lex-rule := const-lex-rule & obj-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD comp ].

intrans-suffix-lex-rule-super := add-only-no-ccont-rule & infl-lex-rule & nom-prefix-rule-dtr & reg-subj-suffix-rule-dtr &
  [ INFLECTED #infl,
    DTR intrans-suffix-rule-dtr &
	[ INFLECTED #infl ] ].

intrans-lex-rule := intrans-suffix-lex-rule-super.

nom-change-lex-rule := same-modified-lex-rule & same-light-lex-rule & same-non-local-lex-rule & same-ctxt-lex-rule & same-agr-lex-rule.

nom-prefix-lex-rule-super := nom-change-lex-rule & infl-lex-rule & reg-subj-suffix-rule-dtr &
  [ SYNSEM.LOCAL.CAT [ HEAD noun &
			    [ FORM nom,
			      MOD < > ],
		       VAL [ SPR < [ LOCAL.CAT.HEAD det,
						    OPT - ] >,
			     SPEC #spec,
			     SUBJ #subj,
			     COMPS #comps ] ],
    C-CONT [ HOOK [ XARG #xarg,
		    LTOP #ltop,
		    INDEX ref-ind & #index ],
	     RELS <! arg1-relation &
		     [ PRED "nominalization_rel",
		       LBL #ltop,
		       ARG0 #index,
		       ARG1 #arg ] !>,
	     HCONS <! !>,
	     ICONS <! !> ],
    INFLECTED #infl,
    DTR nom-prefix-rule-dtr &
	[ SYNSEM.LOCAL [ CAT.VAL [ SPR < >,
				   SUBJ #subj,
				   SPEC #spec,
				   COMPS #comps ],
			 CONT.HOOK [ XARG #xarg,
				     LTOP #arg ] ],
	  INFLECTED #infl ] ].

nominalize-lex-rule := nom-prefix-lex-rule-super.

subj-suffix-lex-rule-super := add-only-no-ccont-rule & durative-prefix-rule-dtr & emph-prefix-rule-dtr & past-suffix-rule-dtr & progressive-prefix-rule-dtr & still-prefix-rule-dtr &
  [ INFLECTED [ SUBJ-SUFFIX-FLAG +,
		OBJ-SUFFIX-FLAG #obj-suffix,
		TRANS-SUFFIX-FLAG #trans-suffix ],
    DTR subj-suffix-rule-dtr &
	[ INFLECTED [ OBJ-SUFFIX-FLAG #obj-suffix,
		      TRANS-SUFFIX-FLAG #trans-suffix ] ] ].

reg-subj-lex-rule := const-lex-rule & subj-suffix-lex-rule-super &
  [ DTR reg-subj-suffix-rule-dtr ].

trans-subj-lex-rule := subj-suffix-lex-rule-super &
  [ DTR trans-subj-suffix-rule-dtr ].

sub-subj-lex-rule := infl-lex-rule & subj-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.HEAD.FORM subord ].

nom-subj-lex-rule := infl-lex-rule & subj-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.HEAD.FORM nom ].

3-reg-subj-lex-rule := reg-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT [ HEAD.FORM finite,
		       VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.PER 3rd ] ].

non3-reg-subj-lex-rule := reg-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT [ HEAD.FORM non3,
		       VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.PER non-3rd ] ].

3-trans-subj-lex-rule := infl-lex-rule & trans-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT [ VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.PER 3rd,
		       HEAD.FORM finite ] ].

non3-trans-subj-lex-rule := const-lex-rule & trans-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT [ VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.PER non-3rd,
		       HEAD.FORM non3 ] ].

1sg-sub-subj-lex-rule := sub-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM sg,
								PER 1st ] ].

1pl-sub-subj-lex-rule := sub-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM pl,
								PER 1st ] ].

2sg-sub-subj-lex-rule := sub-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM sg,
								PER 2nd ] ].

2pl-sub-subj-lex-rule := sub-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM pl,
								PER 2nd ] ].

3sub-subj-lex-rule := sub-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.PER 3rd ].

1sg-nom-subj-lex-rule := nom-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM sg,
				       PER 1st ] ].

1pl-nom-subj-lex-rule := nom-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM pl,
				       PER 1st ] ].

2sg-nom-subj-lex-rule := nom-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM sg,
				       PER 2nd ] ].

2pl-nom-subj-lex-rule := nom-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM pl,
				       PER 2nd ] ].

3-nom-subj-lex-rule := nom-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG.PER 3rd ].

mc-subj-suffix-lex-rule-super := add-only-no-ccont-rule & infl-lex-rule &
  [ INFLECTED.MC-SUBJ-SUFFIX-FLAG +,
    DTR mc-aux-lex ].

mc-subj-lex-rule := mc-subj-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CAT.HEAD.FORM finite ].

1sg-mc-subj-lex-rule := mc-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM sg,
								PER 1st ] ].

1pl-mc-subj-lex-rule := mc-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM pl,
								PER 1st ] ].

2sg-mc-subj-lex-rule := mc-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM sg,
								PER 2nd ] ].

2pl-mc-subj-lex-rule := mc-subj-lex-rule &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CONT.HOOK.INDEX.PNG [ NUM pl,
								PER 2nd ] ].

past-suffix-lex-rule-super := add-only-no-ccont-rule & durative-prefix-rule-dtr & emph-prefix-rule-dtr & infl-lex-rule & progressive-prefix-rule-dtr & still-prefix-rule-dtr &
  [ INFLECTED #infl,
    DTR past-suffix-rule-dtr &
	[ INFLECTED #infl ] ].

past-tense-lex-rule := past-suffix-lex-rule-super &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.E.TENSE past ].

durative-prefix-lex-rule-super := add-only-no-ccont-rule & emph-prefix-rule-dtr & infl-lex-rule & progressive-prefix-rule-dtr & still-prefix-rule-dtr &
  [ INFLECTED #infl,
    DTR durative-prefix-rule-dtr &
	[ INFLECTED #infl ] ].

durative-aspect-lex-rule := durative-prefix-lex-rule-super &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.E.ASPECT dur ].

progressive-prefix-lex-rule-super := add-only-no-ccont-rule & emph-prefix-rule-dtr & infl-lex-rule & still-prefix-rule-dtr &
  [ INFLECTED #infl,
    DTR progressive-prefix-rule-dtr &
	[ INFLECTED #infl ] ].

progressive-aspect-lex-rule := progressive-prefix-lex-rule-super &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.E.ASPECT prog ].

still-prefix-lex-rule-super := add-only-no-ccont-rule & emph-prefix-rule-dtr & infl-lex-rule &
  [ INFLECTED #infl,
    DTR still-prefix-rule-dtr &
	[ INFLECTED #infl ] ].

still-aspect-lex-rule := still-prefix-lex-rule-super &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.E.ASPECT still ].

emph-prefix-lex-rule-super := add-only-no-ccont-rule & infl-lex-rule &
  [ INFLECTED #infl,
    DTR emph-prefix-rule-dtr &
	[ INFLECTED #infl ] ].

emph-aspect-lex-rule := emph-prefix-lex-rule-super &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.E.ASPECT emph ].

adv-lex-rule-super := add-only-no-ccont-rule & const-lex-rule & emph-prefix-rule-dtr & 
  [ INFLECTED.ADVERB-FLAG +,
    DTR adverb-lex ].

adv-head-lex-rule := adv-lex-rule-super & 
  [ SYNSEM.LOCAL.CAT [ HEAD.MOD < [ LOCAL.CAT.HEAD adj ] >,
		       POSTHEAD - ] ].

head-adv-lex-rule := adv-lex-rule-super & 
  [ SYNSEM.LOCAL.CAT [ HEAD.MOD < [ LOCAL.CAT [ HEAD verb & 
						   [ FORM finite ],
						VC + ] ] >,
		       POSTHEAD + ] ].

;;;;;;;;;;;;;;;;;;;
;;; Phrasal Types
;;;;;;;;;;;;;;;;;;;

;Constraint on MC used to be part of matrix.tdl
;;it applies to all wo implementations, except for v2

head-comp-phrase := basic-head-1st-comp-phrase & head-initial &
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < >,
    NON-HEAD-DTR.SYNSEM.SCOPE-ARG - ].

head-comp-phrase-scope := head-valence-phrase & head-compositional & head-initial &  
  [ SYNSEM phr-synsem-min & [ LOCAL [ CAT [ HEAD.AUX -,
					    VAL [ SUBJ #subj, 
						  COMPS #comps, 
						  SPR #spr ], 
					    POSTHEAD #ph, 
					    HC-LIGHT #light ], 
				      CONT.HOOK.INDEX #clause ], 
			       LIGHT #light ], 
    HEAD-DTR.SYNSEM [ LOCAL.CAT [ VAL [ SUBJ #subj, 
					COMPS < #synsem . #comps >, 
					SPR #spr ], 
				  HC-LIGHT #light, 
				  POSTHEAD #ph ] ],  
   NON-HEAD-DTR.SYNSEM canonical-synsem & #synsem & 
		       [ SCOPE-ARG +,
			 LOCAL.CONT.HOOK.CLAUSE-KEY #target ], 
   C-CONT [ RELS <! !>, 
		 HCONS <! !>, 
		 ICONS	<! info-str & [ CLAUSE #clause, 
					TARGET #target ] !> ] ].

head-subj-phrase := decl-head-subj-phrase & head-initial &
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VC + ].

; Rules for building NPs.  Note that the Matrix uses SPR for
; the specifier of nouns and SUBJ for the subject (specifier) of verbs.

head-spec-phrase := basic-head-spec-phrase & head-final & 
 [ HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD.FORM nform ].

basic-head-spec-nom-phrase := head-valence-phrase & phrasal &
			  binary-headed-phrase & no-ccont-rule &
  [ INFLECTED infl-satisfied,
    SYNSEM [ LOCAL.CAT [ VAL [ SUBJ #subj,
			       COMPS #comps,
			       SPR #spr,
			       SPEC #spec ],
			 POSTHEAD #ph,
			 HC-LIGHT - ],
	      MODIFIED #modif ],
    HEAD-DTR [ INFLECTED infl-satisfied,
	       SYNSEM [ LOCAL [ CAT [ HEAD #head,
				      VAL [ SUBJ #subj,
					    COMPS #comps,
					    SPR < #synsem & 
						  canonical-synsem . #spr >,
					    SPEC #spec ],
				      POSTHEAD #ph ],
				CONT.HOOK #hdhook ],
			MODIFIED #hmodif ] ],
    NON-HEAD-DTR.SYNSEM #synsem &
	 [ LOCAL [ CAT [ VAL.SPEC < [ LOCAL [ CAT [ HEAD #head &
							    noun &
							   [ FORM nom ],
						      VAL.COMPS #comps ],
						CONT.HOOK #hdhook ],
					MODIFIED #hmodif ] > ],
		   CONT.HOOK #hook ],
	   MODIFIED #modif ],
    C-CONT.HOOK #hook ].

head-spec-nom-phrase := basic-head-spec-nom-phrase & head-final.

aux-comp-phrase := basic-head-1st-comp-phrase & head-initial &
  [ SYNSEM.LOCAL.CAT [ HEAD verb &
			  [ AUX + ],
		       INIT na ],
    HEAD-DTR.SYNSEM.LOCAL.CAT.INIT na-or-+,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT [ HEAD verb,
				    INIT + ] ].

comp-aux-phrase := basic-head-1st-comp-phrase & head-final &
  [ SYNSEM.LOCAL.CAT [ HEAD verb &
			  [ AUX + ],
		       INIT + ],
    HEAD-DTR.SYNSEM.LOCAL.CAT.INIT -,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT [ HEAD verb,
				    INIT + ] ].
n-bar-predicate-phrase := unary-phrase & 
  [ SYNSEM.LOCAL [ CAT [ HEAD verb &
			     [ MOD < >,
			       FORM non3+finite ],
			 VAL [ COMPS #comps,
			     SPEC #spec,
			     SPR < >,
			     SUBJ < [ LOCAL [ CONT.HOOK.INDEX #arg1,
						  CAT [ HEAD noun,
							VAL.SPR < > ] ] ] > ],
			 INIT + ],
		   AGR #agr,
		   COORD -,
		   COORD-REL #crel ],
    C-CONT [ HOOK [ LTOP #ltop,
		    INDEX #index,
		    XARG #arg1,
		    ICONS-KEY #icons ],
	     RELS <! arg12-ev-relation &
		   [ PRED "_be_v_id_rel",
		     LBL #ltop,
		     ARG0 #index,
		     ARG1 #arg1,
		     ARG2 #arg2 ],
		     quant-relation &
		   [ PRED "exist_q_rel",
		     ARG0 #arg2,
		     RSTR #harg ] !>,
	     HCONS <! qeq &
		    [ HARG #harg, 
		      LARG #larg ] !> ],
    ARGS < [ SYNSEM.LOCAL [ CAT [ HEAD noun,
				  VAL [ SPR cons,
					SPEC #spec,
					COMPS #comps ] ],
			    CONT.HOOK [ INDEX #arg2,
					LTOP #larg,
					ICONS-KEY #icons ],
		   AGR #agr,
		   COORD -,
		   COORD-REL #crel ] ] > ].

np-predicate-phrase := unary-phrase &
  [ SYNSEM.LOCAL [ CAT [ HEAD verb &
			   [ MOD < >,
			     FORM nom ],
		       VAL [ SPR < >,
			     SPEC #spec,
			     COMPS #comps,
			     SUBJ < #subj &
				       [ LOCAL [ CONT.HOOK.INDEX #arg1,
					      CAT [ HEAD noun,
						    VAL [ SPR < >,
							  COMPS < > ] ] ] ] > ],
		       INIT + ],
		   AGR #agr,
		   COORD -,
		   COORD-REL #crel ],
    C-CONT [ HOOK [ LTOP #ltop,
		    INDEX #index,
		    XARG #arg1,
		    ICONS-KEY #icons ],
	     RELS <! arg12-ev-relation &
		   [ PRED "_be_v_id_rel",
		     LBL #ltop,
		     ARG0 #index,
		     ARG1 #arg1,
		     ARG2 #arg2 ] !> ],
    ARGS < [ SYNSEM.LOCAL [ CAT [ HEAD noun,
				  VAL [ SUBJ < #subj >,
					SPR < >,
					SPEC #spec,
					COMPS #comps ] ],
			    CONT.HOOK [ INDEX #arg2,
					ICONS-KEY #icons ],
			    AGR #agr,
			    COORD -,
			    COORD-REL #crel ] ] >].


;;;;;;;;;;;;;;;;;;
;;; Coordination
;;;;;;;;;;;;;;;;;;

;;; Coordination Strategy 1

np1-top-coord-rule := basic-np-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

np1-mid-coord-rule := basic-np-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

np1-bottom-coord-rule := conj-first-bottom-coord-rule & np-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

;;; Coordination Strategy 2

;vp2-top-coord-rule := basic-vp-top-coord-rule & apoly-top-coord-rule &
;  [ SYNSEM.LOCAL.COORD-STRAT "2" ].

;vp2-bottom-coord-rule := conj-first-bottom-coord-rule & vp-bottom-coord-phrase &
;  [ SYNSEM.LOCAL.COORD-STRAT "2" ].

;;; Coordination Strategy 2

s2-top-coord-rule := basic-s-top-coord-rule & apoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "2",
    LCOORD-DTR.SYNSEM.LOCAL.CAT.HEAD.FORM finite ].

s2-bottom-coord-rule := conj-first-bottom-coord-rule & s-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "2",
    NONCONJ-DTR.SYNSEM.LOCAL.CAT [ HEAD.FORM finite,
				   VAL.COMPS < >  ] ].
